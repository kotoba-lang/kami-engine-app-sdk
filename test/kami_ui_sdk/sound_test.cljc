(ns kami-ui-sdk.sound-test
  (:require [clojure.test :refer [deftest testing is]]
            [kami-ui-sdk.sound :as sound]))

(defn- close? [a b] (< (Math/abs (- (double a) (double b))) 1e-6))

(deftest presets-test
  (is (= 15 (count sound/presets)))
  (is (every? vector? (vals sound/presets)))
  (testing "success is an ascending 3-note triad"
    (let [notes (:success sound/presets)]
      (is (= 3 (count notes)))
      (is (apply < (map :freq notes))))))

(deftest list-presets-test
  (is (= (set (keys sound/presets)) (set (sound/list-presets)))))

(deftest envelope-at-test
  (let [spec {:duration 0.1 :volume 0.5 :attack 0.01 :release 0.03}]
    (testing "0 at attack start"
      (is (close? 0.0 (sound/envelope-at spec 0))))
    (testing "reaches full volume at end of attack"
      (is (close? 0.5 (sound/envelope-at spec 0.01))))
    (testing "sustains at volume mid-note"
      (is (close? 0.5 (sound/envelope-at spec 0.05))))
    (testing "decays toward ~0 by end of note"
      (is (< (sound/envelope-at spec 0.099) 0.5))
      (is (> (sound/envelope-at spec 0.099) 0.0)))
    (testing "negative t is silent"
      (is (close? 0.0 (sound/envelope-at spec -1))))))

(deftest freq-at-test
  (testing "no sweep -> constant freq"
    (is (= 800 (sound/freq-at {:freq 800} 0.5))))
  (testing "sweeps from freq to freq-to over duration"
    (let [spec {:freq 1000 :freq-to 500 :duration 0.1}]
      (is (close? 1000.0 (sound/freq-at spec 0)))
      (is (close? 500.0 (sound/freq-at spec 0.1)))
      (is (< (sound/freq-at spec 0.05) 1000))
      (is (> (sound/freq-at spec 0.05) 500))))
  (testing "clamps freq-to at 20Hz floor"
    (let [spec {:freq 100 :freq-to 5 :duration 0.1}]
      (is (close? 20.0 (sound/freq-at spec 0.1))))))

(deftest click-preset-shape-test
  (let [[n] (:click sound/presets)]
    (is (= :sine (:type n)))
    (is (= 800 (:freq n)))))
